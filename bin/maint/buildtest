#! /usr/bin/env tcsh
# 
# Compiles and tests PETSc in our nightly builds.
# Run as buildtest ARCH where ARCH is one of the default PETSc archs
# of a configure arch
#
# Example usage: ${PETSC_DIR}/bin/maint/buildtest solaris
#                ./bin/maint/buildtest solaris
#                ./bin/maint/buildtest 
#
set day=`date | cut -f1 -d" "`
#
# on cygwin - tcsh gives funky paths - so reset it to normal bash one so 'chkpetsc_dir' doesn't complain
set nPETSC_DIR=`pwd | sed s~/cygdrive/h~~`
if ( ! -f ${nPETSC_DIR}/include/petscsys.h ) then
  set maintdir=`dirname $0`
  set nPETSC_DIR=`dirname $maintdir`
  if ( ( ${nPETSC_DIR} !~ /*) || (! -f ${nPETSC_DIR}/include/petscsys.h )) then
    if (${?PETSC_DIR}) then 
      set nPETSC_DIR=${PETSC_DIR}
      if ( ! -f ${nPETSC_DIR}/include/petscsys.h ) then
        echo "Unable to locate PETSc directory, run with full path or in PETSc directory"
        exit
      endif
    else
      echo "Unable to locate PETSc directory, run with full path or in PETSc directory"
      exit
    endif
  endif
endif

if (${#argv} > 0) then 
  set ARCH=${1}
else
  set ARCH=`${nPETSC_DIR}/bin/configarch`
endif
if (${#argv} > 1) then 
  set CONFIGURE_ARGS="${argv[2-]}"
else 
  set CONFIGURE_ARGS=""
endif

set MACH=`hostname`
cd $nPETSC_DIR
rm -f build.log
echo "Build on $MACH $ARCH $nPETSC_DIR `date` " > build.log

# Note use 'hg clean --all' intead of manually deleting seleted old files.
# This also requires 'clean' extension enabled on all nightly build machines.

echo "Cleaning throughly and updating mercurial clone at $nPETSC_DIR" >>& build.log
(hg -q revert -a; hg -q clean --all -X build.log; hg -q pull -u) >>& build.log
(cd config/BuildSystem; hg -q revert -a; hg -q clean --all; hg -q pull -u; cd $nPETSC_DIR) >>& build.log

setenv PETSC_DIR $nPETSC_DIR
set PETSC_ARCH=${ARCH}
set CONFIGURE='yes'
set CONFIGURE_FILE=$PETSC_ARCH
endif

# Run Configure
  ./config/examples/${CONFIGURE_FILE}.py --with-no-output -PETSC_ARCH=$PETSC_ARCH -PETSC_DIR=$nPETSC_DIR ${CONFIGURE_ARGS} >>& build.log
  echo '****************************************************' >>& build.log

make PETSC_ARCH=$PETSC_ARCH PETSC_DIR=$nPETSC_DIR allfortranstubs >>& build.log

make PETSC_ARCH=$PETSC_ARCH PETSC_DIR=$nPETSC_DIR  all test >>& build.log
echo '****************************************************' >>& build.log
echo "Finished Build on $MACH $PETSC_ARCH `date`" >>& build.log
tar rf buildoutput.tar build.log
#
# Run examples
#
set flag=`grep PETSC_WITH_BATCH ${PETSC_DIR}/${PETSC_ARCH}/conf/petscvariables`
if ( $? ) then
  set TESTMODE='testexamples'
else
  set TESTMODE='buildexamples'
endif
#
# check if PETSC_ARCH has name valgrind - if so enable valgrind runs
#
echo $PETSC_ARCH | grep valgrind > /dev/null
if ( $? ) then
  set VALGRIND='DUMMYARGUMENT=foobar'
else
  set VALGRIND=`echo MPIEXEC="${PETSC_DIR}/bin/petscmpiexec -valgrind"`
endif
rm -f examples.log
echo "Build on $MACH $PETSC_ARCH  `date` " > examples.log
make PETSC_ARCH=$PETSC_ARCH PETSC_DIR=$nPETSC_DIR  alltests TESTMODE=$TESTMODE "${VALGRIND}" |& grep -v 'up to date' >>& examples.log
echo '******************************************************' >>& $nPETSC_DIR/examples.log

set GCOV=`grep PETSC_USE_GCOV $nPETSC_DIR/$PETSC_ARCH/include/petscconf.h`
if ( "${GCOV}" != "" ) then
  make PETSC_ARCH=$PETSC_ARCH PETSC_DIR=$nPETSC_DIR gcov  >>& examples.log
endif

echo "Finished Build on $MACH $PETSC_ARCH `date`" >>& $nPETSC_DIR/examples.log
echo 'X******************************************************X' >>& $nPETSC_DIR/examples.log
tar rf buildoutput.tar examples.log


