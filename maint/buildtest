#! /bin/csh
# $Id: buildtest,v 1.2 2001/10/19 16:20:25 petsc Exp $ 
# 
# Compiles and tests PETSc in our nightly builds.
# Run as buildtest ARCH where ARCH is one of the PETSc archs
# This script runs on terra, and uses terra:/sandbox/petsc/petsc-test
#
# Example usage: buildtest solaris
#
set DATAFILESPATH=/home/petsc/datafiles
set NICE=''
set APPEND='>>&'
set RSH='ssh -1'
set RCP='scp -q -B -oProtocol=1'
set CONFIGURE='no'

source /sandbox/petsc/petsc-test/bmake/$1/buildtest

set EJOBS=`grep PETSC_HAVE_ /sandbox/petsc/petsc-test/bmake/$1/packages | grep -v "#" | grep -v '\$' | sed -e "s/^PETSC_HAVE_\([A-Z0-9_]*\) [- a-zA-Z_0-9=]*/\1/g" | grep -v DPETSC`

if ("${BOPT}" == "g_complex") then
  set EJOBS=""
endif
if ("${BOPT}" == "O_complex") then
  set EJOBS=""
endif
#
set RSYNC_DIR=`echo $TMP | sed 's%[^/][^/]*$%%' | sed 's%\/$%%'`
rsync -z -e "$RSH" -C -r --exclude=BitKeeper /sandbox/petsc/petsc-test ${MACH}:$RSYNC_DIR
#if ("${ARCH}" != "t3e") then
#  $RSH $MACH -n "rm -f ${TMP}/lib/lib*/${ARCH}/*" >& /dev/null
#endif


#$RSH $MACH -n "cd $TMP/maint;rm -f build_$ARCH.$BOPT.$MACH.log examples_$ARCH.$BOPT.$MACH.log"
$RSH $MACH -n "cd $TMP/maint;touch build_$ARCH.$BOPT.$MACH.log examples_$ARCH.$BOPT.$MACH.log"

$RSH $MACH -n "cd $TMP;echo 'Build on $MACH $ARCH $BOPT `date` ' $APPEND maint/build_$ARCH.$BOPT.$MACH.log"

# Run Configure
if ("${CONFIGURE}" == "yes") then
  #$RSH $MACH -n "rm -f $TMP/configure.log" >& /dev/null
  $RSH $MACH -n "cd $TMP; config/configure_$ARCH.py $APPEND maint/build_$ARCH.$BOPT.$MACH.log"
  $RSH $MACH -n "cd $TMP;echo '****************************************************' $APPEND maint/build_$ARCH.$BOPT.$MACH.log"
  #$RSH $MACH -n "cd $TMP; cat configure.log $APPEND maint/build_$ARCH.$BOPT.$MACH.log"
  #$RSH $MACH -n "cd $TMP;echo '****************************************************' $APPEND maint/build_$ARCH.$BOPT.$MACH.log"
  $RCP ${MACH}:$TMP/configure.log /home/petsc/logs/nightly/configure__$ARCH.$BOPT.$MACH.log
endif

$RSH $MACH -n "cd $TMP;make PETSC_ARCH=$ARCH PETSC_DIR=$TMP BOPT=$BOPT all $APPEND maint/build_$ARCH.$BOPT.$MACH.log"

$RSH $MACH -n "cd $TMP;echo '****************************************************' $APPEND maint/build_$ARCH.$BOPT.$MACH.log"
$RSH $MACH -n "cd $TMP;echo 'Finished Build on $MACH $ARCH $BOPT `date`' $APPEND maint/build_$ARCH.$BOPT.$MACH.log"
$RCP ${MACH}:$TMP/maint/build_$ARCH.$BOPT.$MACH.log /home/petsc/logs/nightly
chmod a+r /home/petsc/logs/nightly/build_$ARCH.$BOPT.$MACH.log
$RSH $MACH -n "cd $TMP;chmod -R g+w lib" >& /dev/null
#
#  Now run the test examples
#
$RSH $MACH -n "cd $TMP ;echo 'Build on $MACH $ARCH  $BOPT `date` ' $APPEND maint/examples_$ARCH.$BOPT.$MACH.log"
foreach j ($JOBS $EJOBS)
  $RSH $MACH -n "cd $TMP; $NICE make PETSC_ARCH=$ARCH PETSC_DIR=$TMP BOPT=$BOPT ACTION=$TEST$j DATAFILESPATH=$DATAFILESPATH tree | grep -v 'up to date' $APPEND maint/examples_$ARCH.$BOPT.$MACH.log"
  $RSH $MACH -n "cd $TMP;echo '******************************************************' $APPEND maint/examples_$ARCH.$BOPT.$MACH.log"
end
$RSH $MACH -n "cd $TMP ;echo 'Finished Build on $MACH $ARCH $BOPT `date`' $APPEND maint/examples_$ARCH.$BOPT.$MACH.log"
$RSH $MACH -n "cd $TMP ;echo 'X******************************************************X' $APPEND maint/examples_$ARCH.$BOPT.$MACH.log"



